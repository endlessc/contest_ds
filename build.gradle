plugins {
	id "java"
	id "io.freefair.lombok" version "4.1.6" apply false //https://projectlombok.org/setup/gradle
	id "org.springframework.boot" version "2.2.5.RELEASE" apply false
	id "io.spring.dependency-management" version "1.0.9.RELEASE" apply false
}

allprojects {
	group = "com.incar"
	version = "0.1.0-SNAPSHOT"
}

subprojects {
	// https://docs.gradle.org/current/userguide/plugins.html
	if (name.startsWith("ds_pipe") || name.startsWith("ds_web_back")) {
		apply plugin: "java"
		apply plugin: "io.freefair.lombok"
		apply plugin: "org.springframework.boot"
		apply plugin: "io.spring.dependency-management"
	}

	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8

	ext {
		springBootVersion = "2.2.5.RELEASE"
	}

	repositories {
		maven { url "https://maven.aliyun.com/repository/public" }
		maven { url "https://repository.incarcloud.com/content/groups/public" }
	}

	dependencies {
		implementation "org.slf4j:slf4j-api:1.7.28"

		testImplementation "org.junit.jupiter:junit-jupiter-engine:5.5.2"
		testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.5.2"
		testCompile("org.springframework.boot:spring-boot-starter-test:${springBootVersion}") {
			exclude group: 'junit', module: 'junit'
		}
	}

	compileJava {
		options.encoding = "UTF-8"
		options.compilerArgs << "-Xlint:unchecked"
	}

	compileTestJava {
		options.encoding = "UTF-8"
		options.compilerArgs << "-Xlint:unchecked"
	}

	test {
		useJUnitPlatform()
	}

	defaultTasks "assemble"
}
